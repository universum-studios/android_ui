/*
 * =================================================================================================
 *                             Copyright (C) 2017 Universum Studios
 * =================================================================================================
 *         Licensed under the Apache License, Version 2.0 or later (further "License" only).
 * -------------------------------------------------------------------------------------------------
 * You may use this file only in compliance with the License. More details and copy of this License
 * you may obtain at
 *
 * 		http://www.apache.org/licenses/LICENSE-2.0
 *
 * You can redistribute, modify or publish any part of the code written within this file but as it
 * is described in the License, the software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES or CONDITIONS OF ANY KIND.
 *
 * See the License for the specific language governing permissions and limitations under the License.
 * =================================================================================================
 */
apply plugin: 'com.android.library'

/**
 * Library options =================================================================================
 */
android {
    compileSdkVersion Integer.valueOf((String) project.property('android.compileSdkVersion'))
    buildToolsVersion project.property('android.buildToolsVersion')

    defaultConfig {
        minSdkVersion project.property('library.minSdkVersion')
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        modules {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    sourceSets {
        release.java.srcDirs = [
                'src/main/java',
                'src/core/java',
                'src/base/java',
                'src/controller/base/java',
                'src/controller/refresh/java',
                'src/controller/pull/java',
                'src/graphics/base/java',
                'src/graphics/progress/java',
                'src/graphics/refresh/java',
                'src/interpolator/java',
                'src/util/java',
                'src/widget/collection/java',
                'src/widget/common/button/java',
                'src/widget/common/image/java',
                'src/widget/common/menu/java',
                'src/widget/common/progress/java',
                'src/widget/common/selection/java',
                'src/widget/common/text/java',
                'src/widget/input/java',
                'src/widget/layout/base/java',
                'src/widget/layout/refresh/java',
                'src/widget/layout/scroll/java',
                'src/widget/paging/java',
                'src/widget/picker/java',
                'src/widget/web/java'
        ]
        debug.java.srcDirs = release.java.srcDirs
        release.res.srcDirs = [
                'src/main/res',
                'src/core/res',
                'src/base/res',
                'src/controller/refresh/res',
                'src/controller/pull/res',
                'src/widget/collection/res',
                'src/widget/common/button/res',
                'src/widget/common/image/res',
                'src/widget/common/menu/res',
                'src/widget/common/progress/res',
                'src/widget/common/selection/res',
                'src/widget/common/text/res',
                'src/widget/input/res',
                'src/widget/layout/base/res',
                'src/widget/layout/refresh/res',
                'src/widget/layout/scroll/res',
                'src/widget/paging/res',
                'src/widget/picker/res',
                'src/widget/web/res'
        ]
        debug.res.srcDirs = release.res.srcDirs
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

/**
 * Library dependencies ============================================================================
 */
repositories {
    mavenLocal()
}

dependencies {
    /*
     * RELEASE DEPENDENCIES ------------------------------------------------------------------------
     */
    compile releaseDependencies.androidSupportAnnotations
    compile releaseDependencies.androidSupportAppCompatV7
    compile releaseDependencies.androidSupportVectorDrawable
    compile releaseDependencies.androidSupportRecyclerViewV7
    compile releaseDependencies.androidUniversumGraphicsUtil

    /*
     * TEST DEPENDENCIES ---------------------------------------------------------------------------
     */
    // Android Instrumentation test dependencies.
    androidTestCompile testDependencies.androidSupportAnnotations
    androidTestCompile testDependencies.androidRunner
    androidTestCompile testDependencies.androidRules
    androidTestCompile testDependencies.androidEspressoCore
    androidTestCompile testDependencies.androidEspressoIntents
    androidTestCompile(testDependencies.androidEspressoContrib) {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    // Local JUnit test dependencies.
    testCompile testDependencies.junit
    testCompile testDependencies.mockitoCore
}

// Apply PUBLISH script.
apply from: '../publish/publish.gradle'

// Apply CODACY script.
apply from: 'codacy.gradle'