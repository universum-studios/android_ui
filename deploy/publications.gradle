/*
 * =================================================================================================
 *                             Copyright (C) 2017 Universum Studios
 * =================================================================================================
 *         Licensed under the Apache License, Version 2.0 or later (further "License" only).
 * -------------------------------------------------------------------------------------------------
 * You may use this file only in compliance with the License. More details and copy of this License
 * you may obtain at
 *
 * 		http://www.apache.org/licenses/LICENSE-2.0
 *
 * You can redistribute, modify or publish any part of the code written within this file but as it
 * is described in the License, the software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES or CONDITIONS OF ANY KIND.
 *
 * See the License for the specific language governing permissions and limitations under the License.
 * =================================================================================================
 */
apply plugin: 'maven-publish'

def pomConfig = {
    name config.pom.project.name
    description config.pom.project.description
    url config.pom.project.url
    inceptionYear config.pom.project.inceptionYear
    issueManagement {
        system config.pom.project.issueManagement.system
        url config.pom.project.issueManagement.url
    }
    licenses {
        license {
            name config.pom.project.licenses.Apache20.name
            url config.pom.project.licenses.Apache20.url
            distribution config.pom.project.licenses.Apache20.distribution
        }
    }
    organization {
        name config.pom.project.organization.name
        url config.pom.project.organization.url
    }
    developers {
        developer {
            id config.pom.project.developers.albedinsky.id
            name config.pom.project.developers.albedinsky.name
            email config.pom.project.developers.albedinsky.email
            roles {
                role config.pom.project.developers.albedinsky.role
            }
        }
    }
}

publishing {
    publications {
        library(MavenPublication) {
            groupId config.pom.groupId
            artifactId project.property('maven.pom.artifactId')
            version config.pom.version
            artifact bundleRelease
            artifact classesJar
            artifact javadocJar
            artifact sourcesJar
            pom.withXml {
                def projectNode = asNode()
                // Append POM configuration.
                projectNode.children().last() + pomConfig
                // Append dependencies.
                def dependenciesNode = projectNode.appendNode('dependencies')
                // Iterate over the dependencies, adding a <dependency>...</dependency>
                // node for each one that is part of the implementation.
                configurations.implementation.allDependencies.each {
                    if(it.group != null && (it.name != null || it.name != "unspecified") && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                configurations.releaseImplementation.allDependencies.each {
                    if(it.group != null && (it.name != null || it.name != "unspecified") && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}