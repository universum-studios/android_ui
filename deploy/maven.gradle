/*
 * =================================================================================================
 *                             Copyright (C) 2017 Universum Studios
 * =================================================================================================
 *         Licensed under the Apache License, Version 2.0 or later (further "License" only).
 * -------------------------------------------------------------------------------------------------
 * You may use this file only in compliance with the License. More details and copy of this License
 * you may obtain at
 *
 * 		http://www.apache.org/licenses/LICENSE-2.0
 *
 * You can redistribute, modify or publish any part of the code written within this file but as it
 * is described in the License, the software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES or CONDITIONS OF ANY KIND.
 *
 * See the License for the specific language governing permissions and limitations under the License.
 * =================================================================================================
 */
apply plugin: 'maven'

/**
 * Maven configuration =============================================================================
 */
afterEvaluate { project ->
    /**
     * Uploads project's artifacts into the local maven repository.
     * <p>
     * This will also generate a POM file.
     */
    uploadArchives {
        repositories.mavenDeployer {
            repository(url: "file:///${config.maven.localUrl}")
            /**
             * Project artifact --------------------------------------------------------------------
             */
            pom.groupId = config.pom.groupId
            pom.artifactId = project.property('maven.pom.artifactId')
            pom.version = config.pom.version
            pom.packaging = config.pom.packaging
            /**
             * Project description -----------------------------------------------------------------
             */
            pom.project {
                name config.pom.project.name
                description config.pom.project.description
                url config.pom.project.url
                inceptionYear config.pom.project.inceptionYear
                issueManagement {
                    system config.pom.project.issueManagement.system
                    url config.pom.project.issueManagement.url
                }
                licenses {
                    license {
                        name config.pom.project.licenses.Apache20.name
                        url config.pom.project.licenses.Apache20.url
                        distribution config.pom.project.licenses.Apache20.distribution
                    }
                }
                organization {
                    name config.pom.project.organization.name
                    url config.pom.project.organization.url
                }
                developers {
                    developer {
                        id config.pom.project.developers.albedinsky.id
                        name config.pom.project.developers.albedinsky.name
                        email config.pom.project.developers.albedinsky.email
                        roles {
                            role config.pom.project.developers.albedinsky.role
                        }
                    }
                }
            }
        }
    }
}

/**
 * Task that updates library artifacts directory with artifacts from the Maven local directory.
 */
task updateArtifacts() {
    doLast {
        if (config.maven.localUrl != null) {
            final String artifactId = String.valueOf(project.property('maven.pom.artifactId'))
            final String artifactsSourceDir = "${config.maven.localUrl}/universum/studios/android/${artifactId}/${config.pom.version}"
            final String artifactsDestinationDir = "${rootProject.rootDir}/artifacts/universum/studios/android/${artifactId}/${config.pom.version}"
            delete artifactsDestinationDir
            copy {
                from artifactsSourceDir
                into artifactsDestinationDir
            }
        }
    }
}