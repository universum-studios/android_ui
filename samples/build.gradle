/*
 * =================================================================================================
 *                             Copyright (C) 2017 Universum Studios
 * =================================================================================================
 *         Licensed under the Apache License, Version 2.0 or later (further "License" only).
 * -------------------------------------------------------------------------------------------------
 * You may use this file only in compliance with the License. More details and copy of this License
 * you may obtain at
 *
 * 		http://www.apache.org/licenses/LICENSE-2.0
 *
 * You can redistribute, modify or publish any part of the code written within this file but as it
 * is described in the License, the software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES or CONDITIONS OF ANY KIND.
 *
 * See the License for the specific language governing permissions and limitations under the License.
 * =================================================================================================
 */
apply plugin: 'com.android.application'

/**
 * Application options =============================================================================
 */
android {
    compileSdkVersion config.build.compileSdkVersion
    buildToolsVersion config.build.buildToolsVersion

    defaultConfig {
        applicationId project.property('samples.applicationId')
        versionName project.property('samples.versionName')
        versionCode Integer.valueOf((String) project.property('samples.versionCode'))
        minSdkVersion config.build.minSdkVersion
        targetSdkVersion config.build.targetSdkVersion
        vectorDrawables.useSupportLibrary = true
    }

    final String keyDirPath = "${System.getenv('ANDROID_UNIVERSUM_KEYSTORE')}/samples"
    final File keyFile = file("${keyDirPath}/release.jks")
    final File keyPropertiesFile = file("${keyDirPath}/key.properties")
    if (keyFile.exists() && keyPropertiesFile.exists()) {
        final Properties signingProperties = new Properties()
        signingProperties.load(new FileInputStream(keyPropertiesFile))
        //
        // The key.properties file should contain following data (each property at a single line):
        // <pre>
        // store.password=STORE_PASSWORD
        // key.alias=ALIAS_OF_THE_KEY
        // key.password=KEY_PASSWORD
        // </pre>
        //
        signingConfigs {
            release {
                storeFile keyFile
                storePassword signingProperties['store.password']
                keyAlias signingProperties['key.alias']
                keyPassword signingProperties['key.password']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

/**
 * Application dependencies ========================================================================
 */
repositories {
    mavenLocal()
    maven { url repos.bintray.universumStudiosAndroid }
}

dependencies {
    // PRODUCTION ----------------------------------------------------------------------------------
    implementation project(':library')
    implementation deps.androidSupport.design
    implementation deps.androidSupport.vectorDrawable
    implementation deps.androidSupport.recyclerView
    implementation deps.other.butterknife
    annotationProcessor deps.other.butterknifeProcessor
    implementation deps.universumStudios.fontCore
    implementation deps.universumStudios.graphicsColorUtil
    implementation deps.universumStudios.supportSamples
    implementation deps.universumStudios.supportFragments
    implementation deps.universumStudios.widgetAdapters
    implementation deps.universumStudios.logger
    releaseImplementation deps.analytics.leakcanaryAndroidNoOp

    // DEVELOPMENT ---------------------------------------------------------------------------------
    debugImplementation deps.analytics.leakcanaryAndroid

    // TEST ----------------------------------------------------------------------------------------
    // None.
}