/*
 * =================================================================================================
 *                             Copyright (C) 2017 Universum Studios
 * =================================================================================================
 *         Licensed under the Apache License, Version 2.0 or later (further "License" only).
 * -------------------------------------------------------------------------------------------------
 * You may use this file only in compliance with the License. More details and copy of this License
 * you may obtain at
 *
 * 		http://www.apache.org/licenses/LICENSE-2.0
 *
 * You can redistribute, modify or publish any part of the code written within this file but as it
 * is described in the License, the software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES or CONDITIONS OF ANY KIND.
 *
 * See the License for the specific language governing permissions and limitations under the License.
 * =================================================================================================
 */
apply plugin: 'com.android.application'

/**
 * Application options =============================================================================
 */
android {
    compileSdkVersion config.build.compileSdkVersion
    buildToolsVersion config.build.buildToolsVersion

    defaultConfig {
        applicationId project.property('samples.applicationId')
        minSdkVersion config.build.minSdkVersion
        targetSdkVersion config.build.targetSdkVersion
        versionCode Integer.valueOf((String) project.property('samples.versionCode'))
        versionName project.property('samples.versionName')
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    if (project.hasProperty('signing.properties') && new File((String) project.property('signing.properties')).exists()) {
        def Properties properties = new Properties();
        properties.load(new FileInputStream(file(project.property('signing.properties'))))

        signingConfigs {
            release {
                storeFile file(properties['store.file'])
                storePassword properties['store.password']
                keyAlias properties['key.alias']
                keyPassword properties['key.password']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

/**
 * Application dependencies ========================================================================
 */
repositories {
    mavenLocal()
    maven { url repos.bintray.universumStudiosAndroid }
}

dependencies {
    /*
    * RELEASE DEPENDENCIES -------------------------------------------------------------------------
    */
    compile project(':library')
    compile deps.androidSupport.design
    compile deps.androidSupport.vectorDrawable
    compile deps.androidSupport.recyclerView
    compile deps.other.butterknife
    annotationProcessor deps.apt.butterknife
    compile deps.universumStudios.supportSamples
    compile deps.universumStudios.supportFragments
    compile deps.universumStudios.widgetAdapters
    releaseCompile deps.analytics.leakcanaryAndroidNoOp

    /*
    * DEVELOPMENT DEPENDENCIES ---------------------------------------------------------------------
    */
    debugCompile deps.analytics.leakcanaryAndroid
}