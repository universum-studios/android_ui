// Global gradle configuration. This configuration will be used for all sub-projects.
apply from: './configuration.gradle'
apply from: './repositories.gradle'
apply from: './dependencies.gradle'

/**
 * Global build script configuration ===============================================================
 */
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'universum.studios.gradle:github-plugin:1.1.2'
    }
}

/**
 * All projects global configuration ===============================================================
 */
allprojects {
    repositories {
        google()
        jcenter()
    }
}

/**
 * All projects global tasks =======================================================================
 */
/**
 * Task that cleans build directory of the root project.
 */
task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Task that updates library artifacts directory.
 */
task updateArtifacts() {
    subprojects.findAll { if (it.name.startsWith("library")) dependsOn ":${it.name}:updateArtifacts" }
    doLast {
        if (file(repos.project.artifacts).exists()) {
            exec {
                commandLine 'git', 'add', repos.project.artifacts, '-A'
            }
        }
    }
}

/**
 * Task that deploys (uploads) all library modules (except groups) into Maven local repository.
 */
task deployModulesToMavenLocal() {
    group 'deploy'
    description 'Deploys artifacts for all library modules (except groups) into Maven local repository.'
    subprojects.findAll { if (it.name.startsWith("library-") && !it.name.endsWith('_group')) dependsOn ":${it.name}:uploadArchives" }
}

/**
 * Task that deploys (uploads) all library module groups into Maven local repository.
 */
task deployGroupsToMavenLocal() {
    group 'deploy'
    description 'Deploys artifacts for all library module groups into Maven local repository.'
    subprojects.findAll { if (it.name.startsWith("library-") && it.name.endsWith('_group')) dependsOn ":${it.name}:uploadArchives" }
}

/**
 * Task that deploys (uploads) all library modules (except groups) up to the Bintray repository.
 */
task deployModulesToBintray() {
    group 'deploy'
    description 'Deploys artifacts for all library modules (except groups) up to the Bintray repository.'
    subprojects.findAll { if (it.name.startsWith("library-") && !it.name.endsWith('_group')) dependsOn ":${it.name}:bintrayUpload" }
}

/**
 * Task that deploys (uploads) all library module groups up to the Bintray repository.
 */
task deployGroupsToBintray() {
    group 'deploy'
    description 'Deploys artifacts for all library module groups up to the Bintray repository.'
    subprojects.findAll { if (it.name.startsWith("library-") && it.name.endsWith('_group')) dependsOn ":${it.name}:bintrayUpload" }
}

/**
 * Task that deploys (uploads) the primary library module into Maven local repository.
 */
task deployToMavenLocal(dependsOn: ':library:uploadArchives') {
    group 'deploy'
    description 'Deploys primary artifact of the library to the local Maven repository.'
}

/**
 * Task that deploys (uploads) the primary library module up to the Bintray repository.
 */
task deployToBintray(dependsOn: ':library:bintrayUpload') {
    group 'deploy'
    description 'Deploys primary artifact of the library up to the Bintray repository.'
}

/**
 * Task that deploys (uploads) the primary library module up to the GitHub repository.
 */
task deployToGitHub(dependsOn: ':library:githubUploadProductionRelease') {
    group 'deploy'
    description 'Deploys primary artifact of the library up to the GitHub repository.'
}